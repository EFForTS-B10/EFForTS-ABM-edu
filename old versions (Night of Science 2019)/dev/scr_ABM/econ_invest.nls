
;###################################################################################
; INVESTMENT COSTS
;###################################################################################

To-report optimal-invest [landuse management age]
  ;; Report the item age from the investmwent cost list if available, otherwise report the last item from the list
  let invest_function item management [l_mng_invest_function] of a-lut landuse
  let invest (runresult invest_function age)
  report invest
end



To-report investment-costs-of-one-option [option-matrix]
  ;reports investment costs of one option in the current year based on the option matrix which has as rows: field-id, landuse, age, field-size and capital stock
  
  let inv-costs-option 0
  
  let dim matrix:dimensions option-matrix
  let number-of-fields item 1 dim ;number of fields of that household
 
  let field 0
  
  while [field < number-of-fields] ;go through all fields and calculate investment costs under this option
  [
    let inv-costs-this-field 0
    
    let field-id matrix:get option-matrix 0 field 
    let field-age matrix:get option-matrix 2 field 
    let field-size matrix:get option-matrix 3 field
    let field-landuse matrix:get option-matrix 1 field 
   
    set inv-costs-this-field (optimal-invest field-landuse 0 field-age) * field-size    ; <2.0>MANAGE
    
    set inv-costs-option inv-costs-option + inv-costs-this-field
    set field field + 1
  ]
  
  report inv-costs-option   
End  

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

To-report investment-costs-of-one-option-cummulative-3-years [turtle-id option]
  ;calculates investment costs of one option for one household that would occur during the next three years
  let inv-costs 0
  let field-ids h_field_id_list
   
  ;calculate investment costs for all fields under this option
  let number-of-fields length option 
  let field 0
  
  while [field < number-of-fields]
  [
    let inv-costs-this-field 0
    let field-id item field field-ids
    let representant one-of h_patches with [p_id = field-id]
    let current-option item field option
    let field-size [p_fieldsize] of representant
    
    ;; If current option = -1 (stay with current landuse) use p_landuse, otherwise use the new landuse
    let field-landuse ifelse-value (current-option = -1) [[p_landuse] of representant][item field option]
    let field-age ifelse-value (current-option = -1) [[p_age] of representant][0]
    
    let year 0
    while [year < 3] ; discounted costs within three years are calculated
    [
      set inv-costs-this-field inv-costs-this-field + (optimal-invest field-landuse 0 field-age) * (1 + discount-rate)^ year   ; <2.0>MANAGE  
      set year year + 1
    ]
    ;multiply by field size
    set inv-costs-this-field inv-costs-this-field * field-size  
    
    ; update inv costs and jump to next field in the loopd
    set inv-costs inv-costs + inv-costs-this-field
    set field field + 1
  ]
  
  report inv-costs    
End 