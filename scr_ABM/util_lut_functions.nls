
;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
; These util functions can be used to generate the parameter data for the parameter input files:
To-report lut_function_yield_oilpalm [age]
  ;gives potential yield of fresh fruit bunches in MG/patch depending on patch age
  ;function is based on the function of Hoffmann et al. 2014 (see summary-production-data_munir_hoffmann_paper.r)

  let production_hectare 0
  
  let scal 0.3
  let expo 0.7
  let plateau 40
  let slope -0.6
  let intercept 46
  
  if age <= 2 [set production_hectare 0]
  if age > 2 and age <= 7 [set production_hectare scal * exp(expo * age)];#exp_inc(exponent_rounded, scaling_rounded, age)
  if age > 7 and age <= 11[set production_hectare plateau]
  if age > 11 [set production_hectare slope * age + intercept] 
  
  if production_hectare < 0 [set production_hectare 0]

  let production_cell production_hectare * patch_size
 
 report production_cell 
 
End 

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
To-report lut_function_yield_rubber [age]
  ;gives potential yield of rubber in MG/patch depending on patch age
  ;polynomial function
  let quadratic_term -0.007
  let linear_term 0.3
  let constant 2.5
  let production_hectare 0
  
  ifelse age <= 4 
  [
    set production_hectare 0
  ]
  [
    set production_hectare  quadratic_term * age ^ 2 + linear_term * age + constant 
  ]
  if production_hectare < 0 ;avoid negative production for old plantation ages
  [
    set production_hectare 0 
  ]
  
  let production_cell production_hectare * patch_size
  
  report production_cell 
End  

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
To-report lut_function_yield_junglerubber [age]
  
  let production_rubber lut_function_yield_rubber age
  let junglerubber_reduction 0.2
  let production_cell production_rubber * junglerubber_reduction
  report production_cell 
End  

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
To-report lut_function_carbon_oilpalm [age]
  report ((18.95 * age ^(0.5)) * 0.413) * 1.35 * patch_size
end

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
To-report lut_function_carbon_rubber [age]
  report 58.609 * exp(-13.696 * exp(-0.264 * age)) * patch_size
end
;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
To-report lut_function_carbon_junglerubber [age]
  report 70 * patch_size
end

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
To-report lut_function_labor_oilpalm [age]
  ;reports optimal labor in hours/year and cell depending on the age of the plantaion  
  ;parameter values are based on CRC data, see model description
  let opt-labor-ha 0
  let b1 5
  let b2 7
  let b3 25
  let m1 100
  let m2 -80
  let m3 -0.8
  let n1 -230
  let n2 690
  let n3 120
  let plateau 1400  ;was 1100 with the old prices
  if age <= 2 [set opt-labor-ha 0]
  if age > 2 and age <= b1 [set opt-labor-ha m1 * age + n1]
  if age > b1 and age <= b2 [set opt-labor-ha m2 * age + n2]
  if age > b2 [set opt-labor-ha m3 * age + n3]
  if age > b3 [set opt-labor-ha plateau]
  let opt-labor-cell opt-labor-ha * patch_size  
  report opt-labor-cell
End  

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
To-report lut_function_labor_rubber [age]
  ;reports optimal labor in hours/year and cell depending on the age of the plantaion  
  ;parameter values are based on CRC data, see model description
  let b 700
  let opt-labor-ha 0
  if age <= 4 [set opt-labor-ha 0] 
  if age > 4  [set opt-labor-ha b]
  let opt-labor-cell opt-labor-ha * patch_size 
  report opt-labor-cell
End  

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
To-report lut_function_labor_junglerubber [age]
  ;reports optimal labor in hours/year and cell depending on the age of the plantaion  
  let b 700
  let opt-labor-ha 0
  if age <= 4 [set opt-labor-ha 0] 
  if age > 4  [set opt-labor-ha b * 0.25]
  let opt-labor-cell opt-labor-ha * patch_size 
  report opt-labor-cell
End  

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
To-report lut_function_labor_fallow [age]
  ;reports optimal labor in hours/year and cell depending on the age of the plantaion  
  ;parameter values are based on CRC data, see model description
  report 0
End  

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
To-report lut_function_tinput_oilpalm [age]
  ;reports optimal technical input [kg/year] for one cell depending on the age of the plantation
  ;parameter values are based on CRC data, see model description
  let opt-tinput-ha 0
  let m 740
  if age <= 2 [set opt-tinput-ha 0]
  if age > 2 [set opt-tinput-ha 740]
  let opt-tinput-cell opt-tinput-ha * patch_size
  report opt-tinput-cell
End 

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
To-report lut_function_tinput_rubber [age]
  ;reports optimal technical input [kg/year] for one cell depending on the age of the plantation
  ;parameter values are based on CRC data, see model description
  let opt-tinput-ha 0
  let m 150
  if age <= 4 [set opt-tinput-ha 0]
  if age > 4  [set opt-tinput-ha m] 
  let opt-tinput-cell opt-tinput-ha * patch_size
  report opt-tinput-cell
End  

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
To-report lut_function_tinput_junglerubber [age]
  ;reports optimal technical input [kg/year] for one cell depending on the age of the plantation
  ;parameter values are based on CRC data
  let opt-tinput-ha 0
  let m 150
  if age <= 4 [set opt-tinput-ha 0]
  if age > 4  [set opt-tinput-ha  m * 0.25] 
  let opt-tinput-cell opt-tinput-ha * patch_size
  report opt-tinput-cell
End    

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
To-report lut_function_tinput_fallow [age]
  ;reports optimal labor in hours/year and cell depending on the age of the plantaion  
  ;parameter values are based on CRC data, see model description
  report 0
End  

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
To-report lut_function_invest_oilpalm [age]
  ; reports investment costs
  let investment-costs [600 200 150]
  set investment-costs ifelse-value (age < length investment-costs) [(item age investment-costs)][0]
  set investment-costs investment-costs * patch_size 
  report investment-costs
End

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
To-report lut_function_invest_rubber [age]
  ; reports investment costs
  let investment-costs [200 70 70 70 70]
  set investment-costs ifelse-value (age < length investment-costs) [item age investment-costs][0]
  set investment-costs investment-costs * patch_size 
  report investment-costs
End

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
To-report lut_function_invest_junglerubber [age]
  ; reports investment costs
  let investment-costs [200 50 50]
  set investment-costs ifelse-value (age < length investment-costs) [item age investment-costs][0]
  set investment-costs investment-costs * patch_size 
  report investment-costs
End

;~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
To-report lut_function_invest_fallow [age]
  ;reports optimal labor in hours/year and cell depending on the age of the plantaion  
  ;parameter values are based on CRC data, see model description
  report 0
End  
